#!/usr/bin/perl
use strict;
#
# Copyright (c) 2009 Red Hat, Inc.
#
# This software is licensed to you under the GNU General Public License,
# version 2 (GPLv2). There is NO WARRANTY for this software, express or
# implied, including the implied warranties of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE. You should have received a copy of GPLv2
# along with this software; if not, see
# http://www.gnu.org/licenses/old-licenses/gpl-2.0.txt.
#
# Red Hat trademarks are not licensed under GPLv2. No permission is
# granted to use or replicate Red Hat trademarks that are incorporated
# in this software or its documentation.
#

use lib '/etc/rc.d/np.d';
use NOCpulse::NOCpulseini;
use PhysCluster;

use Date::Manip;
use Getopt::Long;

#set the defaults
my $delete_unmatched = 1;
my $delta = "";
my $dry_run = 0;
my $help = 0;

my ($sql, $err, $sth);

GetOptions(
        "delete-unmatched!" => \$delete_unmatched,
        "dry-run" => \$dry_run,
        "delete-before:s" => \$delta,
        "help" => \$help,
        );

if ($help) {
        print <<HELP;
Usage:
  delete-old-probes --no-delete-unmatched

HELP
        exit;
}
if ($dry_run) {
        print "Warning: Dry run active - no changes will be made.\n";
}
 
my $cluster = PhysCluster->newInitialized('/etc/rhn/cluster.ini');
my $localConfig = $cluster->get_LocalConfig;
my $ini = NOCpulse::NOCpulseini->new;

if (%$localConfig) {
        my $config = (values(%$localConfig))[0];
        my $dbd = $config->get_dbd;
        my $dbname = $config->get_dbname;
        my $username = $config->get_username;
        my $password = $config->get_password;
        $ini->connect($dbd, $dbname, $username, $password);
        $ini->fetch_nocpulseini('INTERNAL');
} else {
        print "Error: This script can be run only on monitoring backend.\n";
        exit 1;
}

if ($delete_unmatched) {
        $ini->dbh->begin_work;
        print "Deleting data from already deleted probes...\n";
        $sql = qq|
                delete from time_series 
                where time_series.rowid in (
                        select time_series.rowid from time_series 
                                left join rhn_probe 
                                on SUBSTR(O_ID,INSTR(O_ID,'-')+1,(INSTR(O_ID,'-',INSTR(O_ID,'-')+1)-INSTR(O_ID,'-'))-1) = rhn_probe.recid 
                                where rhn_probe.recid is null
                        )
                |;
        $sth = $ini->dbh->prepare($sql);
        $sth->execute();
        print "\t", $sth->rows, " record deleted.\n";
        $dry_run ? $ini->dbh->rollback : $ini->dbh->commit;
}

if ($delta) {
        # delete probes older then $delta
        my $date=DateCalc("today", "- $delta", \$err);
        if ($err > 0) {
                print STDERR "Error: $delta is not valid delta of date.\n";
                exit 1;
        }
        print UnixDate($date,"Deleting probes data older than %T %b %e, %Y...\n");
        $sql = qq|delete from time_series where entry_time < ?|;
        $ini->dbh->begin_work;
        $sth = $ini->dbh->prepare($sql);
        $sth->execute(UnixDate($date,"%s"));
        print "\t", $sth->rows, " record deleted.\n";
        $dry_run ? $ini->dbh->rollback : $ini->dbh->commit;
} else {
        print "No date delta specified. I do not know what to delete.\n";
}

=pod

=head1 NAME

delete-old-probes - Delete unused and old data from monitoring probes.

=head1 SYNOPSIS

delete-old-probes [OPTIONS]

=head1 DESCRIPTION


=head1 OPTIONS

--delete-unmatched
--no-delete-unmatched
        Delete data from probes, which have been already deleted. Default is to delete them.

--help
        Display this help.

=head1 EXAMPLES

=head1 AUTHOR

Miroslav Such√Ω <msuchy@redhat.com>

=head1 COPYRIGHT AND LICENSE

Copyright (c) 2009 Red Hat, Inc.
Released under GNU General Public License, version 2 (GPLv2).

=cut

